{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "24068523",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Takes as input a square matrix Q, and outputs a primitive vector v such that v.transpose() * Q * v == 0 if such a v exists, and -1 otherwise.\n",
    "# The vector v is chosen randomly by the following method:\n",
    "# A matrix Ts with abs(Ts.det()) == 1 is chosen randomly\n",
    "# Then v is set to qfsolve of Ts.transpose() * Q * Ts, which is a matrix congruent to Q\n",
    "# Ts * v / gcd(v) is returned\n",
    "def randomized_qfsolve(Q, random_steps, random_matrix_entry_size):\n",
    "    from sage.quadratic_forms.qfsolve import qfsolve\n",
    "    n = Q.dimensions()[0]\n",
    "    Ts = identity_matrix(n)\n",
    "    v = None\n",
    "    counter = 0\n",
    "    # Sometimes there is a PARI exception for reasons unclear to me; this is why whe while, try, except construction here is needed\n",
    "    while (v == None) and (counter < 20):\n",
    "        counter += 1\n",
    "        for i in range(random_steps):\n",
    "            T = matrix(ZZ,n,n,lambda i, j: 0 if i < j\n",
    "                       else (-1)^(int(random()*2)) if i == j\n",
    "                       else int(random() * 2 * random_matrix_entry_size) - random_matrix_entry_size)\n",
    "            Ts = Ts * (T if random() > .5 else T.transpose())\n",
    "        try:\n",
    "            v = qfsolve(Ts.transpose() * Q * Ts)\n",
    "        except:\n",
    "            v = None\n",
    "    # qfsolve returns an integer if no solution exists\n",
    "    if (v == None) or (type(v) == sage.rings.integer.Integer): return -1\n",
    "    v = v / gcd(v)\n",
    "    return Ts * v\n",
    "    \n",
    "# Takes as input an integer square matrix A that is the Seifert matrix of a knot or link.\n",
    "# Returns a list [n, T], where n is a non-negative integer and T is a matrix of same size as A with abs(T.det()) == 1\n",
    "# such that the 2n x 2n upper left submatrix of T * A * T.transpose() is a Seifert matrix of a knot with Alexander polynomial 1\n",
    "# The function tries to find such an n as large as possible.\n",
    "# The returned n is a lower bound for g(A) - g_alg(A), where g(A) is the genus of the Seifert surface with Seifert matrix A, i.e. g(A) = rank(A - A.transpose()) / 2,\n",
    "# and g_alg(A) is defined in Peter Feller, Lukas Lewark: \"On classical upper bounds for slice genera\"\n",
    "def galg(A, random_steps = 10, random_matrix_entry_size = 10):\n",
    "    n = A.dimensions()[0]\n",
    "    # The algorithm tries to apply a congruence creating a 2 x 2 submatrix with Alexander polynomial 1.\n",
    "    # If n < 2, no such submatrix can exist. If n == 2, such a submatrix must be the whole matrix, so we just check if the whole matrix has Alexander polynomial 1.\n",
    "    if (n < 2):\n",
    "        return [0, identity_matrix(n)]\n",
    "    if (n == 2):\n",
    "        if ((A - A.transpose()).det() == 1) and ((A + A.transpose()).det() == 1):\n",
    "            return [1, identity_matrix(2)]\n",
    "        return [0, identity_matrix(2)]\n",
    "    # Find isotropic vector v\n",
    "    v = randomized_qfsolve(A + A.transpose(), random_steps, random_matrix_entry_size);\n",
    "    if (v in [-1,-2]):\n",
    "        return [0, identity_matrix(n)]\n",
    "    # Complete v to a basis, so that A1[0,0] == 0\n",
    "    T1 = matrix(ZZ, v).smith_form()[2].inverse();\n",
    "    A1 = T1 * A * T1.transpose();\n",
    "    # Apply Euclid's algorithm to first row ignoring the first column, so that the first row of A2 is [0,1,0,...,0], if possible (this may fail)\n",
    "    v1 = A1[0,range(1,n)]\n",
    "    X, Y, Z = matrix(ZZ, v1).smith_form()\n",
    "    c = matrix(ZZ, [1 if i == 0 else 0 for i in range(n-1)])\n",
    "    if (X != c):\n",
    "        return [0, identity_matrix(n)]\n",
    "    T2 = block_matrix([[identity_matrix(1),0],[0,Z.transpose()]])\n",
    "    A2 = T2 * A1 * T2.transpose();\n",
    "    # Apply Euclid's algorithm to the first column ignoring the first two rows, so that the first column of A3 is [0,x,y,0,...,0]\n",
    "    v2 = A2[range(2,n),0]\n",
    "    T3 = block_matrix([[identity_matrix(2),0],[0,matrix(ZZ, v2).smith_form()[1]]])\n",
    "    A3 = T3 * A2 * T3.transpose();\n",
    "    # If y divides x (see previos comment), change the first column so that the first column of A4 is [0,0,y,0,...,0]\n",
    "    if (A3[1,0] != 0) and ((A3[2,0] == 0) or (A3[1,0] % A3[2,0] != 0)):\n",
    "        return [0, identity_matrix(n)]\n",
    "    T4 = identity_matrix(n)\n",
    "    if (A3[1,0] != 0):\n",
    "        T4[1,2] = -A3[1,0] // A3[2,0];\n",
    "    A4 = T4 * A3 * T4.transpose();\n",
    "    # Use the 1 at (0,1) to clear out the second column, so that the second column of A5 is [1,0,...,0]\n",
    "    T5 = matrix(ZZ, n, n, lambda i, j: 1 if i == j else -A4[i,1] if j == 0 else 0)\n",
    "    A5 = T5 * A4 * T4.transpose();\n",
    "    \n",
    "    # Continue recursively in the submatrix obtained by deleting the first three (!) rows and columns  \n",
    "    r = galg(A5[range(3,n),range(3,n)])\n",
    "    T6 = block_matrix([[identity_matrix(3),0],[0,r[1]]])\n",
    "    # Use T7 to permute basis vectors, sending the third basis vector to the end\n",
    "    T7 = matrix(ZZ,n,n,lambda i,j: 1 if ([i,j] in [[0,0],[1,1],[n-1,2]] + [[k-1,k] for k in range(3,n)]) else 0)\n",
    "    fullT = T7 * T6 * T5 * T4 * T3 * T2 * T1;\n",
    "    # Now fullT * A * tullT.transpose() has a 2(1 + r[0]) x 2(1 + r[0]) upper left submatrix that looks like this (in case of 4 x 4):\n",
    "    # 0 1 * *\n",
    "    # 0 0 * *\n",
    "    # 0 0 0 1 \n",
    "    # 0 0 0 0\n",
    "    # and similarly for larger matrices. * signifies arbitrary elements. This submatrix has Alexander polynomial 1.\n",
    "    return [1 + r[0], fullT]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3ad237cf",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 9.7",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.9"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
